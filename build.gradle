buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

println("Local debug: ${local_debug.toBoolean()}")

version = "${haruhicore_version}"
group = 'com.haruhifanclub.mods.haruhimod'
archivesBaseName = "haruhimod-${minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file('run').canonicalPath
            property 'file.encoding', 'UTF-8'
            property 'user.language', 'en'
            property 'log4j.configurationFile', 'log4j2.xml'
            mods {
                haruhicore {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run').canonicalPath
            property 'file.encoding', 'UTF-8'
            property 'user.language', 'en'
            property 'log4j.configurationFile', 'log4j2.xml'
            mods {
                haruhicore {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.pkg.github.com/auioc/arnicalib-mcmod"
        credentials {
            username = System.getenv("GITHUB_ACTOR") ? System.getenv("GITHUB_ACTOR") : System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    if (local_debug.toBoolean()){
        implementation fileTree(dir: 'libs', include: ['*/*-forgelib.jar', '*/*-api.jar'])
    } else {
        implementation "org.auioc.mods.arnicalib:arnicalib-${minecraft_version}:${arnicalib_version}:forgelib"
    }

    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_version}")
}

ext {
    manifest_attributes = [
        "Specification-Title": 'haruhimod',
        "Specification-Vendor": 'Haruhifanclub',
        "Specification-Version": '1',
        "Implementation-Title": project.name,
        "Implementation-Version": "${version}",
        "Implementation-Vendor" :'Haruhifanclub',
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "HaruhiCore-Version": "haruhicore-${minecraft_version}-${haruhicore_version}-${getVar('CI_VERSION')}"
    ]
}

jar {
    manifest {attributes(manifest_attributes)}
}

jar.finalizedBy('reobfJar')

task forgelibJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.allJava
    classifier = 'forgelib'
    manifest {attributes(manifest_attributes)}
}

tasks.build.dependsOn('forgelibJar')

publishing {}

def getVar(String var_name) {
    return System.getenv(var_name)
}
