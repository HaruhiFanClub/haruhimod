apply from: "$rootDir/gradle/utils.gradle"

def getBuildVersion() {
    if (getEnvAsBoolean('CI')) {
        if (getEnvAsBoolean('GITHUB_ACTIONS')) {
            def dev =  getEnvAsBoolean('IS_RELEASE') ? '' : 'dev-'
            def sha =  System.getenv('GITHUB_SHA').substring(0, 7)
            def buildNumber = System.getenv('GITHUB_RUN_NUMBER')
            return "${dev}rev.${sha}-build.${buildNumber}"
        } else {
            return System.getenv('CI_VERSION')
        }
    } else {
        def sha = getExecOutput(['git', 'rev-parse', '--verify', '--short', 'HEAD'])
        def dirty = getExecOutput(['git', 'status', '--short']).isEmpty() ? '' : '-dirty'
        return "dev-rev.${sha}-build.0${dirty}"
    }
}


project.ext.baseVersion = "${minecraft_version}-${project.modVersion}"
project.ext.fullVersion = "${baseVersion}-${getBuildVersion()}"
project.version = getEnvAsBoolean('IS_RELEASE') ? baseVersion : fullVersion

logger.lifecycle("Project version: ${project.version}")
logger.lifecycle("Full version: ${fullVersion}")
